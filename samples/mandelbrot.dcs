func to_density_char(d)
    ret case
    when d > 8.0 then ' '
    when d > 4.0 then '.'
    when d > 2.0 then '*'
    else              '+'
    end
end

func mandel_converge'(real, imag, iters, creal, cimag)
    ret iters if iters > 255.0 || real*real + imag*imag >= 4.0

    ret mandel_converge'(
        real*real - imag*imag + creal,
        2.0*real*imag + cimag,
        iters+1.0,
        creal,
        cimag
    )
end

func mandel_converge(real, imag)
    ret mandel_converge'(real, imag, 0.0, real, imag)
end

func mandel'(xmin, xmax, xstep, ymin, ymax, ystep)
    var y := ymin
    for y < ymax
        let
            var x := xmin
        in begin
            for x < xmax
                mandel_converge(x, y).to_density_char.print
                x += xstep
            end
            ()
        end
        println("")
        y += ystep
    end
end

func mandel(realstart, imagstart, realmag, imagmag)
    mandel'(realstart, realstart+realmag*78.0, realmag, imagstart, imagstart + imagmag*40.0, imagmag)
end

func main
    mandel(-2.3, -1.3, 0.05, 0.07)
end

